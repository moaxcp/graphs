dependencies {
    api project(':graphs-core')
    api project(':graphs-greenrobot')
    api project(':graphs-truth')
    api project(':graphs-graphviz')
    api project(':graphs-testframework')
    testImplementation 'org.jmockit:jmockit:1.49'
}

test {
    jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
}

patchModules.config = [
        "truth=truth-java8-extension-1.1.3.jar"
]

jacocoTestReport {
    additionalSourceDirs files(project(':graphs-core').sourceSets.main.java.srcDirs)
    additionalClassDirs files(project(':graphs-core').sourceSets.main.output)
    additionalSourceDirs files(project(':graphs-greenrobot').sourceSets.main.java.srcDirs)
    additionalClassDirs files(project(':graphs-greenrobot').sourceSets.main.output)
}

compileTestJava {
    moduleOptions {
        addReads = [
            'com.github.moaxcp.graphs.test' : 'truth,org.junit.jupiter.params'
        ]
    }
}

test {
    moduleOptions {
        addReads = [
            'com.github.moaxcp.graphs.test': 'nl.jqno.equalsverifier,truth,org.junit.jupiter.params'
        ]
        addOpens = [
            'com.github.moaxcp.graphs.core/com.github.moaxcp.graphs' : 'nl.jqno.equalsverifier',
            'com.github.moaxcp.graphs.greenrobot/com.github.moaxcp.graphs.greenrobot' : 'nl.jqno.equalsverifier'
        ]
    }
}

delombok {
    //delombok does not appear to support split package in truth and truth-extensions
    enabled = false
    modulePath.from(configurations.runtimeClasspath)
}

javadoc.enabled = false