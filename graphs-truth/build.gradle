plugins {
    id 'maven-publish'
    id 'signing'
}

description = 'Truth extensions for test assertions.'

dependencies {
    api project(':graphs-core')
    api project(':graphs-greenrobot')
    api project(':graphs-guava')
    api 'com.google.truth:truth:1.1.3'
    api 'com.google.truth.extensions:truth-java8-extension:1.1.3'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.moaxcp.graphs'
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/graphs'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/graphs.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/graphs.git'
                    url = 'https://github.com/moaxcp/graphs'
                }
            }
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/moaxcp/graphs")
                    credentials {
                        username = project.findProperty('github.user')
                        password = project.findProperty('github.key')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

patchModules.config = [
        "truth=truth-java8-extension-1.1.3.jar"
]

delombok {
    //delombok does not appear to support split package in truth and truth-extensions
    enabled = false
    modulePath.from(configurations.runtimeClasspath)
}

javadoc {
    source = sourceSets.main.allSource
}