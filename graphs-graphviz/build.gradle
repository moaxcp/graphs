plugins {
    id 'maven-publish'
    id 'signing'
}

description = 'supports converting graphs to dot format and images'

sourceSets {
    greenrobotGif {
        java {
            srcDir 'src/greenrobot-gif/java'
        }
    }
}

java {
    registerFeature('greenrobotGif') {
        usingSourceSet(sourceSets.greenrobotGif)
    }
}

dependencies {
    api project(':graphs-core')
    implementation 'com.github.moaxcp:gif-builder:0.2.0'
    greenrobotGifApi project(':graphs-greenrobot')
    greenrobotGifImplementation 'org.greenrobot:eventbus-java:3.3.1' //why doesn't this work?
    greenrobotGifImplementation project(':graphs-graphviz')
    greenrobotGifImplementation 'com.github.moaxcp:gif-builder:0.2.0' //why is this needed?
    testImplementation project(':graphs-testframework')
    testImplementation(project(':graphs-graphviz')) {
        capabilities {
            requireCapability 'graphs:graphs-graphviz-greenrobot-gif'
        }
    }
}

patchModules.config = [
    "truth=truth-java8-extension-1.1.3.jar"
]

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.moaxcp.graphs'
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/graphs'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/graphs.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/graphs.git'
                    url = 'https://github.com/moaxcp/graphs'
                }
            }
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/moaxcp/graphs")
                    credentials {
                        username = project.findProperty('github.user')
                        password = project.findProperty('github.key')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

compileTestJava {
    moduleOptions {
        addReads = [
            'com.github.moaxcp.graphs.graphviz' : 'com.github.moaxcp.graphs.testframework,eventbus.java,com.github.moaxcp.graphs.greenrobot,truth'
        ]

        addModules = ['com.github.moaxcp.graphs.testframework']
    }
}

test {
    moduleOptions {
        addReads = [
            'com.github.moaxcp.graphs.graphviz' : 'com.github.moaxcp.graphs.testframework,eventbus.java,com.github.moaxcp.graphs.greenrobot,truth,org.junit.jupiter.params'
        ]
    }
}

delombok {
    modulePath.from(configurations.runtimeClasspath)
}