plugins {
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'org.ajoberstar.reckon' version '0.9.0'
    id 'io.codearte.nexus-staging' version '0.20.0'
    id 'org.sonarqube' version '2.7'
}

group = 'com.github.moaxcp'
description = 'dynamic graphs supporting streams and an EventBus'

reckon {
    scopeFromProp()
    snapshotFromProp()
}

nexusStaging {
    username = System.getProperty('nexus.username')
    password = System.getProperty('nexus.password')
}

allprojects {
    apply plugin:'jacoco'
    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = '0.8.4'
    }
}

subprojects {
    apply plugin:'java-library'

    sourceCompatibility = 11
    targetCompatibility = 11

    javadoc {
        options.addBooleanOption('html5', true)
        options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        options.links 'http://greenrobot.org/files/eventbus/javadoc/3.0/'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
        testImplementation 'com.google.truth:truth:0.42'
        testImplementation 'com.google.truth.extensions:truth-java8-extension:0.42'
        testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.0.3'
        testImplementation 'org.assertj:assertj-core:3.12.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }

        reports {
            html.enabled = true
        }

        systemProperty "com.google.common.truth.disable_stack_trace_cleaning", "true"
    }
}