plugins {
    id 'maven-publish'
    id 'signing'
    id 'org.ajoberstar.git-publish' version '2.0.0'
    id 'me.champeau.gradle.jmh' version '0.4.8'
}

patchModules.config = [
        "truth=truth-java8-extension-0.42.jar"
]

dependencies {
    jmh 'com.google.guava:guava:27.0.1-jre'
}

gitPublish {
    repoUri = 'https://github.com/moaxcp/graphs.git'
    branch = 'gh-pages'
    contents {
        from 'build/docs'
        from 'build/reports'
    }
    commitMessage = 'Publishing from gradle.'
}

jmh {
//    include = ['findLastEdge']
    timeUnit = 'ms'
//    jvmArgs = ['-ea']
//    iterations = 1
//    fork = 1
//    failOnError = true
//    warmupIterations = 1
//    timeout = '10s'
//    timeOnIteration = '3s'
//    benchmarkMode = ['thrpt']
    resultsFile = project.file("${project.projectDir}/reports/jmh/optimized.txt")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'graphs'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/graphs'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/graphs.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/graphs.git'
                    url = 'https://github.com/moaxcp/graphs'
                }
            }
            repositories {
                maven {
                    def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                    def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username System.getProperty('nexus.username')
                        password System.getProperty('nexus.password')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

task mergeJacoco(type: JacocoMerge, dependsOn:':graphs-test:test') {
    destinationFile = file("$buildDir/jacoco/test.exec")
    executionData file("$buildDir/jacoco/test.exec"), file("${project(':graphs-test').buildDir}/jacoco/test.exec")
}

test {
    finalizedBy mergeJacoco
}