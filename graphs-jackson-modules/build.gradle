plugins {
    id 'maven-publish'
    id 'signing'
}

description = 'jackson modules for serialization'

patchModules.config = [
    "truth=truth-java8-extension-1.1.3.jar"
]

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.moaxcp.graphs'
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/graphs'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/graphs.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/graphs.git'
                    url = 'https://github.com/moaxcp/graphs'
                }
            }
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/moaxcp/graphs")
                    credentials {
                        username = project.findProperty('github.user')
                        password = project.findProperty('github.key')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

jacocoTestReport {
    dependsOn ':graphs-test:test'
    executionData("${project(':graphs-test').buildDir}/jacoco/test.exec")
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml,${project(':graphs-test').buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

compileTestJava {
    moduleOptions {
        addReads = [
            'com.github.moaxcp.graphs.jackson' : 'truth,org.junit.jupiter.params'
        ]

        addModules = ['truth', 'org.junit.jupiter.params']
    }
}

test {
    moduleOptions {
        addReads = [
            'com.github.moaxcp.graphs.core' : 'truth,org.junit.jupiter.params'
        ]
    }
}


dependencies {
    api project(':graphs-core')
    api 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
}