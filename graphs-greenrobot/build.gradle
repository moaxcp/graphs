plugins {
    id 'maven-publish'
    id 'signing'
}

description = 'graph implementations which send events to the greenrobot EventBus.'

dependencies {
    api project(':graphs-core')
    api 'org.greenrobot:eventbus-java:3.3.1'
    testImplementation project(':graphs-truth')
    testImplementation project(':graphs-testframework')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.moaxcp.graphs'
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/graphs'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/graphs.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/graphs.git'
                    url = 'https://github.com/moaxcp/graphs'
                }
            }
            repositories {
                maven {
                    name = "OSS"
                    def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                    def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = project.findProperty('nexus.username')
                        password = project.findProperty('nexus.password')
                    }
                }
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/moaxcp/graphs")
                    credentials {
                        username = project.findProperty('github.user')
                        password = project.findProperty('github.key')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

patchModules.config = [
    "truth=truth-java8-extension-1.1.3.jar"
]

compileTestJava {
    moduleOptions {
        addReads = [
            'com.github.moaxcp.graphs.greenrobot' : 'com.github.moaxcp.graphs.testframework,com.github.moaxcp.graphs.truth'
        ]

        addModules = ['com.github.moaxcp.graphs.testframework', 'com.github.moaxcp.graphs.truth']
    }
}

test {
    moduleOptions {
        addReads = [
            'com.github.moaxcp.graphs.greenrobot': 'com.github.moaxcp.graphs.testframework,com.github.moaxcp.graphs.truth'
        ]

        addOpens = [
            'com.github.moaxcp.graphs.greenrobot/com.github.moaxcp.graphs.greenrobot' : 'nl.jqno.equalsverifier',
            'com.github.moaxcp.graphs.core/com.github.moaxcp.graphs' : 'nl.jqno.equalsverifier'
        ]
    }
}

jacocoTestReport {
    dependsOn ':graphs-test:test'
    executionData("${project(':graphs-test').buildDir}/jacoco/test.exec")
    additionalSourceDirs files(project(':graphs-core').sourceSets.main.java.srcDirs)
    additionalClassDirs files(project(':graphs-core').sourceSets.main.output)
}

delombok {
    modulePath.from(configurations.runtimeClasspath)
}